#define ENABLE_SHIP_COORDS
#define ENABLE_SCREEN_UV
#define USE_DEFAULT_VERT
#include "./Data/base_shipquad.shader"

Texture2D _maskTexture;
SamplerState _maskTexture_SS;

Texture2D _noiseTexture2;
SamplerState _noiseTexture2_SS;

Texture2D _capturedBackBuffer;
SamplerState _capturedBackBuffer_SS;

float4 _hotColor = 255;
float4 _coldColor = 255;
float _normalIntensity;

float _camScale;

PIX_OUTPUT pix(in GEOM_OUTPUT input) : SV_TARGET
{
	float intensity = input.color.a;
	float clampedDoubleIntensity = saturate(intensity * 2);
	float mask = _maskTexture.Sample(_maskTexture_SS, input.uv).r * clampedDoubleIntensity;
	if (mask <= 0)
		discard;

	float TEX_SCALE1 = 0.08;
	float TEX_SCALE2 = 0.02;
	float scrollMul = 0.3;
	float2 noise1UVs = float2(input.shipLocation.x * TEX_SCALE1, (input.shipLocation.y * TEX_SCALE1) + (scrollMul * _gameTime));
	float noiseTex = 1 - _texture.Sample(_texture_SS, noise1UVs).r;
	float distortionStrength = 0.3;
	float2 noise2UVs = float2(input.shipLocation.x * TEX_SCALE2, (input.shipLocation.y * TEX_SCALE2) - (scrollMul * _gameTime) + (noiseTex * distortionStrength));
	float noiseTex2 = 1 - _noiseTexture2.Sample(_noiseTexture2_SS, noise2UVs).r;
	float baseNoise = noiseTex * noiseTex2;
	
#ifndef SIMPLE
	float screenDistortionStrength = ((2 * noiseTex2) - 1) * 0.002 * clampedDoubleIntensity;
	float2 distortionUVs = float2(input.screenUV.x, input.screenUV.y + screenDistortionStrength);
	float4 rawNormals = _normalsTarget.Sample(_normalsTarget_SS, distortionUVs);
	float3 baseNormal = colorToNormals(rawNormals.rgb);
	float3 normal = baseNormal;
	normal.z = baseNoise * _normalIntensity;
	normal = normalize(normal);
	float edges = saturate(dot(normal, float3(0, 0, 1)));
	float3 backBuffer = _capturedBackBuffer.Sample(_capturedBackBuffer_SS, distortionUVs).rgb;
#else
	float edges = 0;
#endif

	float squareIntensity = saturate(intensity * intensity);
	float4 col = lerp(_coldColor, _hotColor, baseNoise * (1 - edges) * squareIntensity);
	col *= 1 - edges;
	col.rgb = col.rgb * mask;

#ifndef SIMPLE
	float3 addColor = float3(lerp(_coldColor.rgb, _hotColor.rgb, squareIntensity * baseNoise * mask) * 0.9 * intensity * rawNormals.a);
	float a = (mask * 0.7) + (mask * baseNoise * 0.5);
	return float4(backBuffer.rgb + col.rgb + addColor, a);
#else
	float3 addColor = float3(lerp(_coldColor.rgb, _hotColor.rgb, squareIntensity * baseNoise * mask) * 0.9 * intensity);
	float a = (mask * 0.7) + (mask * baseNoise * 0.5);
	return float4((col.rgb + addColor) * a, 1);
#endif
}